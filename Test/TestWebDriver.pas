{ W3C Webdriver library for Delphi

  Copyright (C) 2021 Giandomenico De Sanctis gidesay@yahoo.com

  This library is free software; you can redistribute it and/or modify it
  under the terms of the GNU Library General Public License as published by
  the Free Software Foundation; either version 2 of the License, or (at your
  option) any later version.

  This program is distributed in the hope that it will be useful, but WITHOUT
  ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
  FITNESS FOR A PARTICULAR PURPOSE. See the GNU Library General Public License
  for more details.

  You should have received a copy of the GNU Library General Public License
  along with this library; if not, write to the Free Software Foundation,
  Inc., 51 Franklin Street - Fifth Floor, Boston, MA 02110-1335, USA.
}

unit TestWebDriver;
{

  Delphi DUnit Test Case
  ----------------------
  This unit contains a skeleton test case class generated by the Test Case Wizard.
  Modify the generated code to correctly setup and call the methods from the unit
  being tested.

}

interface

uses
  Windows, TlHelp32, TestFramework, System.Classes, System.SysUtils, Vcl.Forms,
  JsonDataObjects,
  Webdriver4D,  WebDriverSpec, WebClasses, WebDriverConst,
  WD_http,
//Delphi versione > XE2
{$IF CompilerVersion > 23.0}
  wd_httpDelphi;
{$ELSE}
  WD_httpDelphi_XE2;
{$IFEND}
const
  ROOT_DIR       = '..\..\..';
  TEST_FILES_DIR = '.\';
  TEST_EXT_URL   = 'http://www.google.com';
  TEST_URL       = 'file:///%s/test_webdriver.html';
  TEST_XPATH     = '/html/body/div[1]/div[3]/form/div[1]/div[1]/div[1]/div/div[2]/input';
  TEST_CSS       = '.FPdoLc > center:nth-child(1) > input:nth-child(1)';
type

  TestTWebDriver = class(TTestCase)
  private
    function prepareTestUrl(): string;
  strict protected
    FCMD: TDelphiCommand;
    FWD: TWebDriver;
  public
    procedure CheckHasError;
    function getValue(elem: TElement): string;  virtual;
  published
    procedure TestClearAllSession;
    procedure TestDeleteSession;
    procedure TestExecuteScript;
    procedure TestElementExecuteScript;
    procedure TestGetAllCookies;
    procedure TestGetURL;
    procedure TestNotExistUrl;
    procedure TestNewSession;
    procedure TestGetAllSessions;
    procedure TestGetCurrentWindowHandle;
    procedure TestRefresh;
    procedure TestQuit;
    procedure TestScreenShot;
    procedure TestGetElement;
    procedure TestCloseWindow;
    procedure TestFindElementByID;
    procedure TestFindElementByTag;
    procedure TestFindElementByClassName;
    procedure TestFindElementByLinkText;
    procedure TestFindElementByXPath;
    procedure TestFindElementByCSS;

    procedure TestFindElementsByTag;
    procedure TestFindElementsByXPath;
    procedure TestFindElementsByClassName;
    procedure TestFindElementsByCSS;

    procedure TestGetElementAttribute;
    procedure TestGetElementCssValue;
    procedure TestGetElementSelected;
    procedure TestGetElementText;
    procedure TestGetElementTagname;
    procedure TestGetElementDisplayed;

    procedure TestSelectByText;
    procedure TestSelectedOptions;
    procedure TestSelectIsMultiple;
    procedure TestSelectOptionsCount;

    procedure TestTableRows;

    procedure TestElementClick;
    procedure TestImplicitly_Wait;
    procedure TestElement_screenshot;
    procedure TestSendKey;
    procedure TestElementClear;
    procedure TestSetWindowSize; virtual;
    procedure TestWindowMaximize; virtual;
    procedure TestSwitchToFrame;

    procedure TestAlertText;
    procedure TestAlertAccept;
    procedure TestAlertDismiss;

    procedure TestMouseAction;
    procedure TestKeyboardAction;

  end;



type
  TestFirefoxDriver = class(TestTWebDriver)
  strict private
    procedure StartFireFox;
  private
  public
    procedure SetUp; override;
    procedure TearDown; override;
    function getValue(elem: TElement): string; override;
  published
  end;

  TestChromeDriver = class(TestTWebDriver)
  private
    procedure StartChromeDriver;
  public
    procedure SetUp; override;
    procedure TearDown; override;
  published
//    procedure TestStartChromeDriver;
  end;

  TestEdgeDriver = class(TestTWebDriver)
  private
    FCMD: TDelphiCommand;
    procedure StartEdgeDriver;
  public
    procedure SetUp; override;
    procedure TearDown; override;
    function getValue(elem: TElement): string; override;
  published
  end;


implementation

uses
  Vcl.Imaging.pngimage, Vcl.Dialogs, System.StrUtils, System.Generics.Collections;


function FindTask(ExeFileName: string): Cardinal;
const
  PROCESS_TERMINATE = $0001;
var
  ContinueLoop: BOOL;
  FSnapshotHandle: THandle;
  FProcessEntry32: TProcessEntry32;
begin
  Result := 0;
  FSnapshotHandle := CreateToolhelp32Snapshot(TH32CS_SNAPPROCESS, 0);
  FProcessEntry32.dwSize := SizeOf(FProcessEntry32);
  ContinueLoop := Process32First(FSnapshotHandle, FProcessEntry32);

  while Integer(ContinueLoop) <> 0 do
  begin
    if ((UpperCase(ExtractFileName(FProcessEntry32.szExeFile)) =
                                             UpperCase(ExeFileName))
      or (UpperCase(FProcessEntry32.szExeFile) =
                                             UpperCase(ExeFileName))) then
    begin
      Result := FProcessEntry32.th32ProcessID;
      Break;
    end;
    ContinueLoop := Process32Next(FSnapshotHandle, FProcessEntry32);
  end;
  CloseHandle(FSnapshotHandle);
end;

function TestTWebDriver.prepareTestUrl(): string;
begin
    result := format(TEST_URL,[extractfiledir(application.ExeName)]);
    result := ReplaceStr(result, '\', '/');
end;

function TestTWebDriver.getValue(elem: TElement): string;
begin
   result := elem.Attribute('value') ;
end;


procedure TestTWebDriver.CheckHasError;
begin
  CheckEquals(FWD.HasError, false, FWD.ErrorMessage);
end;



procedure TestTWebDriver.TestClearAllSession;
var
  ses2: string;
begin
  // open a second session
  ses2:=FWD.NewSession();
  Sleep(1000);
  CheckHasError;
  FWD.CloseWindow(ses2);
  FWD.DeleteSession(ses2);
  FWD.Clear;


  Sleep(500);
  CheckHasError;
  Check(FindTask(FWD.browserexe)=0, 'Clear: Not all sessions for '+ FWD.browserexe+' closed');
end;

procedure TestTWebDriver.TestDeleteSession;
begin
  FWD.DeleteSession;
  Sleep(500);
  Check(FindTask(FWD.browserexe)=0, 'DeleteSession: session for '+ FWD.browserexe +' not closed');
end;

procedure TestTWebDriver.TestExecuteScript;
var
  xxx: string;
begin
  FWD.Implicitly_Wait(3000);
  FWD.GetURL(TEST_EXT_URL);
  xxx := FWD.ExecuteScript('return document.head.innerHTML');
  Check(Pos('<title>Google</title>', xxx)>0, 'ExecuteScript: html source not containing title "Google"');
end;

procedure TestTWebDriver.TestGetAllCookies;
var
  Cookies: string;
  lst: TStringList;
begin

  FWD.GetURL(TEST_EXT_URL);
  FWD.Implicitly_Wait(1000);


  Cookies := FWD.GetAllCookieJsonArray;
  Check(FWD.HasError=False,'GetAllCookies: error '+fwd.ErrorMessage );
  try
    lst := TStringList.Create;
    lst.Text := Cookies;
    lst.SaveToFile(TEST_FILES_DIR +  'test_allcookies.txt');
  finally
    FreeAndnil(lst);
  end;

end;

procedure TestTWebDriver.TestGetURL;
begin
  FWD.GetURL(TEST_EXT_URL);

  Check(FWD.HasError=False,'GetURL: error '+fwd.ErrorMessage );

end;

procedure TestTWebDriver.TestNotExistUrl;
begin
  try
    FWD.GetURL('http://www.notexistingxxx.com');
  except
    // nop
  end;

  Check(FWD.HasError=true,'GetNotExistURL: error '+fwd.ErrorMessage );

end;

procedure TestTWebDriver.TestNewSession;
begin
  FWD.Clear;
  FWD.NewSession;

  CheckHasError;
  Check(FWD.SessionID<>'', 'NewSession: no session id created');
end;

procedure TestTWebDriver.TestScreenShot;
var
  filename: string;
  dt: TDateTimeInfoRec;
  tt: TDateTime;
begin

  Sleep(500);
  FWD.GetURL(TEST_EXT_URL);
  FWD.WaitForLoaded;

  filename := TEST_FILES_DIR + 'test_screenshot.png';
  DeleteFile(filename);
  tt := Now();
  FWD.Save_screenshot(filename);

  FileGetDateTimeInfo(filename, dt );
  Check((dt.CreationTime - tt) <5, 'ScreenShot: incorrect file generated, DATE: '+DateTimeToStr(dt.CreationTime));
end;

procedure TestTWebDriver.TestGetElement;
var
  Element: TElement;
begin
  Sleep(500);
  FWD.GetURL(TEST_EXT_URL);
  FWD.Implicitly_Wait(3000);

  // various Google page elements
  Element := nil;
  Element := FWD.FindElementByXPath(TEST_XPATH);
  CheckHasError;
  Element.Free;


  Element := nil;
  Element := FWD.FindElementByClassName('gLFyf');
  CheckHasError;
  Element.Free;

  Element := nil;
  Element := FWD.FindElementByName('q');
  CheckHasError;
  Element.Free;

  Element := nil;
  Element := FWD.FindElementByLinkText('Gmail');
  CheckHasError;
  Element.Free;
end;



procedure TestTWebDriver.TestElement_screenshot;
var
  Element: TElement;
  filename: string;
  dt: TDateTimeInfoRec;
  tt: TDateTime;

begin
  Sleep(500);

  FWD.GetURL(prepareTestUrl);
  FWD.Implicitly_Wait(500);

  filename := TEST_FILES_DIR + 'test_element_screenshot.png';
  DeleteFile(filename);

  Element := FWD.FindElementByID('sele');
  tt:= Now();

  FWD.Element_ScreenShot(Element, filename);

  FileGetDateTimeInfo(filename, dt );

  Check((dt.CreationTime - tt)<3, 'Element_ScreenShot: incorrect file generated, file DATE: '+dateTimeToStr(dt.CreationTime)+ ' date: ' + TimeToStr(tt));
end;

procedure TestTWebDriver.TestSendKey;
var
  Element: TElement;
  v, at: string;
begin
  Sleep(500);


  FWD.GetURL(TEST_EXT_URL);

  Sleep(3000);

  Element := FWD.FindElementByXPath(TEST_XPATH);
  v := 'ferrari italia';
  FWD.SendKey(Element, v);
  Element.executeScript('this.blur();');

  Sleep(1000);
  at :=  getValue(Element);
  // Firefox return the original value (visible getting the element outerHTML),
  // not the current value. In Firefox use this JS line:
  //      at := Element.executeScript('return this.value;');
  Element.Free;

  Check(at=v, 'Sendkey: value error: <<'+at+'>>');
end;

procedure TestTWebDriver.TestElementClear;
var
  Element: TElement;
  v, at: string;
begin
  Sleep(500);


  FWD.GetURL(TEST_EXT_URL);

  Sleep(3000);

  Element := FWD.FindElementByXPath(TEST_XPATH);
  v := 'ferrari italia';
  FWD.SendKey(Element, v);
  Element.executeScript('this.blur();');

  Sleep(1000);
  FWD.ElementClear(Element);
  Sleep(1000);
  at :=  getValue(Element);
  Element.Free;

  Check(at='', 'ElementClear: value not empty: <<'+at+'>>');
end;


procedure TestTWebDriver.TestSetWindowSize;
const
  REQ = '640x480';
  REQ_W = 640;
  REQ_H = 480;
var
  w, h: Integer;
  pw, ph: Single;
begin
  FWD.Set_Window_Size(640, 480);
  Sleep(500);
  w:= StrToInt(FWD.ExecuteScript('return (window.outerWidth);'));
  h:= StrToInt(FWD.ExecuteScript('return (window.outerHeight);'));
  pw := w/REQ_w;
  ph := h/REQ_H;
  Check(  ((0.95<=pw) and (pw<=1.05) and (0.95<=ph) and (ph<=1.05)),
         'SetWindowSize: different window size: '+inttostr(w)+'x'+inttostr(h)+' requested: '+req);
end;

procedure TestTWebDriver.TestWindowMaximize;
var
  w, h: Integer;
  pw, ph: Single;
begin
  FWD.WindowMaximize;
  CheckHasError;
  w:= StrToInt(FWD.ExecuteScript('return (window.outerWidth);'));
  h:= StrToInt(FWD.ExecuteScript('return (window.outerHeight);'));

  pw := w/screen.Width;
  ph := h/screen.Height;
  Check(  ((0.95<=pw) and (pw<=1.05) and (0.95<=ph) and (ph<=1.05)),
         'WindowMaximize: window not maximized to screen: '+inttostr(w)+'x'+inttostr(h)+' screen: '+
            IntToStr(Screen.Width)+'x'+IntToStr(Screen.Height));


end;



procedure TestTWebDriver.TestCloseWindow;
begin
  FWD.CloseWindow();
  Sleep(500);

  CheckHasError;
  Check(FindTask(FWD.browserexe)=0, 'Closewindow: Not all windows for '+ FWD.browserexe+' closed');
end;

procedure TestTWebDriver.TestElementExecuteScript;
var
  element: TElement;
  xxx: string;
begin
  Sleep(500);
  FWD.GetURL(TEST_EXT_URL);
  FWD.Implicitly_Wait(3000);

  // Google research string input field
  Element := FWD.FindElementByXPath(TEST_XPATH);
  //GC(element);
  xxx := element.ExecuteScript('return (this.tagName);');
  Check(xxx='INPUT', 'ElementExecuteScript: result invalid ->'+xxx);
end;

procedure TestTWebDriver.TestFindElementByID;
var
  element: TElement;
begin
  Sleep(500);
  FWD.GetURL(prepareTestUrl);
  FWD.Implicitly_Wait(1000);

  Element := FWD.FindElementByID('testalert');
  CheckHasError;
  Check(element<>nil, 'Element not found');
end;

procedure TestTWebDriver.TestFindElementByTag;
var
  element: TElement;
begin
  Sleep(500);
  FWD.GetURL(TEST_EXT_URL);
  FWD.Implicitly_Wait(3000);

  Element := nil;
  Element := FWD.FindElementByTag('head');
  CheckHasError;
  Check(element<>nil, 'Element not found');
end;

procedure TestTWebDriver.TestFindElementByClassName;
var
  element: TElement;
begin
  Sleep(500);
  FWD.GetURL(TEST_EXT_URL);
  FWD.Implicitly_Wait(3000);

  Element := nil;
  Element := FWD.FindElementByClassName('gLFyf');
  CheckHasError;
  Check(element<>nil, 'Element not found');

end;


procedure TestTWebDriver.TestFindElementByLinkText;
var
  element: TElement;
begin
  Sleep(500);
  FWD.GetURL(TEST_EXT_URL);
  FWD.Implicitly_Wait(3000);

  Element := nil;
  Element := FWD.FindElementByLinkText('Gmail');
  CheckHasError;
  Check(element<>nil, 'Element not found');
  element.Free;
end;

procedure TestTWebDriver.TestFindElementByXPath;
var
  element: TElement;
begin
  Sleep(500);
  FWD.GetURL(TEST_EXT_URL);
  FWD.Implicitly_Wait(3000);

  Element := nil;
  Element := FWD.FindElementByXPath(TEST_XPATH);


  CheckHasError;
  Check(element<>nil, 'Element not found');
  element.Free;
end;

procedure TestTWebDriver.TestFindElementByCSS;
var
  element: TElement;
begin
  Sleep(500);
  FWD.GetURL(TEST_EXT_URL);
  FWD.Implicitly_Wait(3000);

  Element := nil;
  // search button
  Element := FWD.FindElementByCSS(TEST_CSS);
  CheckHasError;
  Check(element<>nil, 'Element not found');
  element.Free;
end;

procedure TestTWebDriver.TestGetCurrentWindowHandle;
var
  ReturnValue: string;
begin
  ReturnValue := FWD.GetCurrentWindowHandle;
  CheckHasError;
  Check(ReturnValue<>'' , 'No window handle returned');

end;

procedure TestTWebDriver.TestGetElementAttribute;
var
  element: TElement;
  vv: string;
begin
  Sleep(500);
  FWD.GetURL(TEST_EXT_URL);
  FWD.Implicitly_Wait(3000);

  Element := nil;
  // submit button
  Element := FWD.FindElementByCSS(TEST_CSS);
  vv := '';
  vv := FWD.GetElementAttribute(Element, 'type');
  CheckHasError;
  Check(vv = 'submit','Getelementattribute: wrong attribute (type="'+vv+'"');
end;



procedure TestTWebDriver.TestElementClick;
var
  Element: TElement;
begin
  Sleep(500);
  FWD.GetURL(TEST_EXT_URL);
  FWD.Implicitly_Wait(3000);

  // Google research string input field
  Element := FWD.FindElementByXPath(TEST_XPATH);
  FWD.SendKey(Element,'ferrari');
  Sleep(500);
  Element.executeScript('this.blur();');
  Element.Free;
  Element := nil;
  Element := FWD.FindElementByName('btnK');
  FWD.ElementClick(Element);
  Sleep(1000);
//  CheckHasError;
  Element.Free;
  Check(FWD.FindElementByXPath('//h3[text()="Sito ufficiale del marchio Ferrari"]')<>nil, 'ElementClick: click not done');
end;


procedure TestTWebDriver.TestFindElementsByXPath;
var
  elements: TObjectList<TElement>;
begin
  Sleep(500);
  FWD.GetURL(TEST_EXT_URL);
  FWD.Implicitly_Wait(3000);

  elements := nil;
  elements := FWD.FindElementsByXPath('//div');
  CheckHasError;
  Check(elements.Count>0, 'FindElementsByXPath: elements not found');
  Check((elements.Items[0].tagname='div') and (elements.Items[elements.Count-1].tagname='div'),
          'FindElementsByXPath: elements not of XPath expr. "//div"');
  elements.Free;
end;

procedure TestTWebDriver.TestFindElementsByTag;
var
  elements: TObjectList<TElement>;
begin
  Sleep(500);
  FWD.GetURL(TEST_EXT_URL);
  FWD.Implicitly_Wait(3000);

  elements := nil;
  elements := FWD.FindElementsByTag('input');
  CheckHasError;
  Check(elements.Count>0, 'FindElementsByTag: elements not found');
  Check((elements.Items[0].tagname='input') and (elements.Items[elements.Count-1].tagname='input'),
          'FindElementsByTag: elements not of tag "input"');
  elements.Free;

end;

procedure TestTWebDriver.TestFindElementsByCSS;
var
  elements: TObjectList<TElement>;
begin
  Sleep(500);
  FWD.GetURL(TEST_EXT_URL);
  FWD.Implicitly_Wait(3000);

  elements := nil;
  elements := FWD.FindElementsByCSS('div');
  CheckHasError;
  Check(elements.Count>0, 'FindElementsByCSS: elements not found');
  Check((elements.Items[0].tagname='div') and (elements.Items[elements.Count-1].tagname='div'),
          'FindElementsByCSS: elements not of CSS expr. "div"');
  elements.Free;
end;


procedure TestTWebDriver.TestFindElementsByClassName;
var
  elements: TObjectList<TElement>;
  val1, val2: string;
begin
  Sleep(500);
  FWD.GetURL(TEST_EXT_URL);
  FWD.Implicitly_Wait(3000);

  elements := nil;
  elements := FWD.FindElementsByClassName('gsfi');
  CheckHasError;
  Check(elements.Count>0, 'FindElementsByClassName: elements not found');
  val1 := elements.Items[0].Attribute('class');
  val2 := elements.Items[elements.Count-1].Attribute('class');
  Check((Pos('gsfi', val1)>0) and (pos('gsfi', val2)>0),
          'FindElementsByClassName: elements not of class "gsfi", val1: '+val1+' val2: '+val2);
  elements.Free;
end;

procedure TestTWebDriver.TestGetAllSessions;
var
  Sessions: string;
  ses2: string;
  Json: TJsonArray;
begin
  // open a second session
  ses2:=FWD.NewSession();
  CheckHasError;


  Sleep(500);
  sessions := FWD.GetAllSessions;
  CheckHasError;
  Check(Sessions<>'', 'GetAllSessions: no sessions returned ');
  if Sessions <> '' then
  begin
      Json := TJsonArray.Create;
      Json.FromJSON(sessions);
      Check(Json.Count=2, 'GetAllSessions: sessions count not equal to 2:  ' + IntToStr(Json.Count));
      Json.Free;
  end;
  FWD.CloseWindow(ses2);
  FWD.DeleteSession(ses2);
  CheckHasError;
 end;



procedure TestTWebDriver.TestImplicitly_Wait;
begin
  FWD.Implicitly_Wait(1000);
  FWD.GetURL(TEST_EXT_URL);

  CheckHasError;
end;

procedure TestTWebDriver.TestQuit;
begin
  FWD.Quit;
  Sleep(500);
  CheckHasError;
  Check(FindTask(FWD.browserexe)=0, 'Quit: browser '+ FWD.browserexe+' not closed');
end;

procedure TestTWebDriver.TestRefresh;
var
  Element:TElement;
  vv:string;
begin
  Sleep(500);
  FWD.GetURL(TEST_EXT_URL);
  FWD.Implicitly_Wait(3000);

  // Google research string input field
  Element := FWD.FindElementByXPath(TEST_XPATH);
  Element.SendKey('dalkgjldfjlgkjdlkd');
  Sleep(500);
  Element.executeScript('this.blur();');
  Element.Free;

  FWD.Refresh();
  CheckHasError;
  Element := FWD.FindElementByXPath(TEST_XPATH);
  vv := getValue(Element);
  Element.Free;

  Check(vv='','Refresh: test field not empty: '+vv);
end;


procedure  TestTWebDriver.TestGetElementCssValue;
var
  element: TElement;
  vv: string;
begin
  Sleep(500);
  FWD.GetURL(TEST_EXT_URL);
  FWD.Implicitly_Wait(3000);

  Element := nil;
  // submit button
  Element := FWD.FindElementByCSS(TEST_CSS);
  vv := '';
  vv := FWD.GetElementCssValue(Element, 'font-size');
  element.Free;
  CheckHasError;
  Check(vv = '14px','GetelementCssValue: wrong property value (property "border"="'+vv+'")');

end;

procedure  TestTWebDriver.TestGetElementText;
var
  element: TElement;
  vv: string;
begin
  Sleep(500);
  FWD.GetURL(TEST_EXT_URL);
  FWD.Implicitly_Wait(3000);

  Element := nil;
  Element := FWD.FindElementByXPath('//a[contains(.,"Pubblicit")]');
  vv := '';
  vv := FWD.GetElementText(Element);
  element.Free;
  CheckHasError;
  Check(vv = 'Pubblicità','GetelementText: wrong text value: "'+vv+'")');

end;

procedure  TestTWebDriver.TestGetElementTagname;
var
  element: TElement;
  vv: string;
begin
  Sleep(500);
  FWD.GetURL(TEST_EXT_URL);
  FWD.Implicitly_Wait(3000);

  Element := nil;
  Element := FWD.FindElementByCSS(TEST_CSS);
  vv := '';
  vv := FWD.GetElementTagname(Element);
  element.Free;
  CheckHasError;
  Check(vv = 'input','GetelementTagname: wrong tag name: "'+vv+'")');

end;

procedure  TestTWebDriver.TestGetElementDisplayed;
var
  element: TElement;
  vv: string;
begin
  FWD.GetURL(prepareTestUrl);
  Sleep(800);

  Element := nil;
  Element := FWD.FindElementByID('notenabled');
  vv := '';
  vv := FWD.GetElementDisplayed(element);
  element.Free;
  CheckHasError;
  Check(vv = 'false','GetelementDisplayed: wrong not selected value: '+vv);


end;



procedure  TestTWebDriver.TestGetElementSelected;
var
  element: TElement;
  vv: string;
begin
  Sleep(500);
  FWD.GetURL(TEST_EXT_URL);
  FWD.Implicitly_Wait(3000);

  Element := nil;
  Element := FWD.FindElementByXPath(TEST_XPATH);
  vv := '';
  vv := FWD.GetElementSelected(element);
  element.Free;
  CheckHasError;
  Check(vv = 'false','GetelementSelected: wrong not selected value: '+vv);


end;

procedure TestTWebDriver.TestSelectByText;
const
  OPTVAL = 'val3';
var
  Element:TElement;
  vv:string;
begin
  FWD.GetURL(prepareTestUrl);
  Sleep(800);

  Element :=FWD.FindElementByID('selesingle');
  Element.selectByText(OPTVAL);
  Sleep(100);
  vv :=Element.executeScript('return (this.options[this.selectedIndex].text);');
  element.Free;

  Check(vv=OPTVAL,'SelectByText: select element not changed, value: '+vv);

end;

procedure TestTWebDriver.TestSelectedOptions;
const
  OPTVAL = 'text2';
var
  Element:TElement;
  vv:string;
begin
  FWD.GetURL(prepareTestUrl);
  Sleep(800);
  Element :=FWD.FindElementByID('sele');
  vv := Element.SelectedOptionText();
  Element.Free;
  Check(vv=OPTVAL,'SelectedOptions: wrong selected options , value: '+vv);

end;

procedure TestTWebDriver.TestSelectIsMultiple;
var
  Element:TElement;
  vv:Boolean;
begin
  FWD.GetURL(prepareTestUrl);
  Sleep(800);
  Element :=FWD.FindElementByID('sele');
  vv := Element.SelectIsMultiple;
  Element.Free;
  Check(vv=true,'SelectIsMultiple: wrong multiple property , value: ' + BoolToStr(vv));

  Element :=FWD.FindElementByID('selesingle');
  vv := Element.SelectIsMultiple;
  Element.Free;
  Check(vv=false,'SelectIsMultiple: wrong not multiple property , value: '+ BoolToStr(vv) );

end;

procedure TestTWebDriver.TestSelectOptionsCount;
var
  Element:TElement;
  vv:Integer;
begin
  FWD.GetURL(prepareTestUrl);
  Sleep(800);
  Element :=FWD.FindElementByID('sele');
  vv := Element.SelectOptionsCount;
  Element.Free;
  Check(vv=3,'SelectOptionsCount: wrong options count , value: ' + IntToStr(vv));

end;

procedure TestTWebDriver.TestTableRows;
var
  Element:TElement;
  vv:Integer;
begin
  FWD.GetURL(prepareTestUrl);
  Sleep(800);
  Element :=FWD.FindElementByID('testtable');
  vv := Element.TableRows;
  Element.Free;
  Check(vv=7,'TableRows: wrong rows count , value: ' + IntToStr(vv));

end;


procedure TestTWebDriver.TestSwitchToFrame;
var
  element: TElement;
begin
  FWD.GetURL(prepareTestUrl);
  FWD.Implicitly_Wait(500);

  Element := nil;
  Element := FWD.FindElementByXPath('//iframe[1]');

  FWD.SwitchToFrame(element);
  CheckHasError;
  element.Free;
  Element := nil;
  Element := FWD.FindElementByID('testalert');

  Check(element<>nil,'SwitchToFrame: element in new frame not found ');
  Check(element.text='Test iframe', 'SwitchToFrame: wrong text of element in new frame  ');
  element.Free;

  FWD.SwitchToDefaultContent;
  CheckHasError;
  Element := nil;
  Element := FWD.FindElementByXPath('//iframe[1]');

  Check(element<>nil,'SwitchToDefaultContent: element in top context/frame not found ');
  element.Free;

end;


procedure TestTWebDriver.TestAlertText;
var
  Element:TElement;
  ale: TAlert;
  vv:string;
begin
  FWD.GetURL(prepareTestUrl);
  Sleep(800);
  Element :=FWD.FindElementByID('testalert');
  Element.Click;
  Element.Free;
  ale := FWD.SwitchToAlert;
  vv := ale.text;
  ale.Free;
  Check(vv='test webdriver alert','AlertText: wrong text , value: ' + vv);

end;

procedure TestTWebDriver.TestAlertAccept;
var
  Element:TElement;
  ale: TAlert;
begin
  FWD.GetURL(prepareTestUrl);
  Sleep(800);
  Element :=FWD.FindElementByID('testalert');
  Element.Click;
  Element.Free;
  Sleep(1000);
  ale := FWD.SwitchToAlert;
  CheckHasError;
  ale.Accept;
  ale.Free;
  ale:=nil;
  try
    ale := FWD.SwitchToAlert;
  except
    // ok, alert not found
  end;
  Check(ale=nil,'AlertAccept: alert not closed');

end;

procedure TestTWebDriver.TestAlertDismiss;
var
  Element:TElement;
  ale: TAlert;
begin
  FWD.GetURL(prepareTestUrl);
  Sleep(800);
  Element :=FWD.FindElementByID('testalert');
  Element.Click;
  Element.Free;
  Sleep(1000);
  ale := FWD.SwitchToAlert;
  CheckHasError;
  ale.Dismiss;
  ale.Free;
  ale:=nil;
  try
    ale := FWD.SwitchToAlert;
  except
    // ok, alert not found
  end;
  Check(ale=nil,'AlertDismiss: alert not closed');


end;

procedure TestTWebDriver.TestMouseAction;
var
  Element:TElement;
  ma, ma1, ma2: TMouseAction;
  maList: TMouseActionContainer;

begin
  maList := TMouseActionContainer.create('mouse1', aptMouse);


  try
    FWD.GetURL(prepareTestUrl);
    Sleep(800);
    Element :=FWD.FindElementByID('testalert');


// esegue un clic sul pulsante
    ma := TMouseAction.create(apPointerMove);
    ma.duration := 500;
    ma.x := 10;
    ma.y := 10;
    ma.origin := Element.ElementID;
    maList.Add(ma);


    ma1 := TMouseAction.create(apPointerDown);
    ma1.duration := 150;
    maList.Add(ma1);

    ma2 := TMouseAction.create(apPointerUp);
    ma2.duration := 150;
    maList.Add(ma2);



    FWD.processActions(maList);

    sleep(3000);


    CheckHasError;


  finally
    maList.Free;
    Element.Free;

  end;
end;

procedure TestTWebDriver.TestKeyboardAction;
var
  Element:TElement;
  ma, maP,maP1, maP2,
  ma2: TKeyboardAction;
  maList: TKeyboardActionContainer;

  vv: string;
begin
  maList := TKeyboardActionContainer.create('key1');


  try
    FWD.GetURL(prepareTestUrl);
    Sleep(800);
    Element :=FWD.FindElementByID('testkey');
    Element.executeScript('this.focus();');


// invia un carattere ad un campo text
    ma := TKeyboardAction.create(akKeyDown, 'X');
    maList.Add(ma);


    maP := TKeyboardAction.create(akPause, #0);
    maP1 := TKeyboardAction.create(akPause, #0);
    maP2 := TKeyboardAction.create(akPause, #0);
    // 3 pauses
    maList.Add(maP);
    maList.Add(maP1);
    maList.Add(maP2);

    ma2 := TKeyboardAction.create(akKeyUp, 'X');
    maList.Add(ma2);



    FWD.processActions(maList);

    sleep(1000);

    CheckHasError;
    vv:= getValue(Element);

    Check(vv='X', 'KeyboardAction: text field value not valid: '+vv);

  finally
    maList.Free;
    Element.Free;

  end;
end;
{**************************************************************************}

{***************************************************************************}

{***************************************************************************}
procedure TestFirefoxDriver.SetUp;
begin
  FCMD :=TDelphiCommand.Create(nil);
  StartFireFox;

end;

procedure TestFirefoxDriver.TearDown;
begin
  if Assigned(FWD) then
    FreeAndNil(FWD);
  if Assigned(FCMD) then
    FreeAndNil(FCMD);
end;

procedure TestFirefoxDriver.StartFireFox;

begin
  FWD :=TFireFoxDriver.Create(nil);
  FWD.Port :=4444;
  FWD.Cmd :=FCMD;
  FWD.DriverDir := ROOT_DIR+  '\WebDriver\';
  FWD.StartDriver();
  FWD.NewSession;
end;

function TestFirefoxDriver.getValue(elem: TElement): string;
begin
   result := elem.PropertyHtml('value') ;
end;
{***************************************************************************}
procedure TestChromeDriver.SetUp;
begin
  FCMD :=TDelphiCommand.Create(nil);
  StartChromeDriver;
end;

procedure TestChromeDriver.TearDown;
begin
  FreeAndNil(FWD);
  if Assigned(FCMD) then
    FreeAndNil(FCMD);

end;


procedure TestChromeDriver.StartChromeDriver;
var
  Chrome:TChromeDriver;
begin
  Chrome :=TChromeDriver.Create(nil);
  FWD :=Chrome;
  Chrome.Port :=6666;
  Chrome.Cmd :=FCMD;
  Chrome.Timeout := 60000;
  Chrome.DriverDir := ROOT_DIR + '\WebDriver\';
  Chrome.StartDriver();
  Chrome.NewSession();


end;

{***************************************************************************}
procedure TestEdgeDriver.SetUp;
begin
  FCMD :=TDelphiCommand.Create(nil);
  StartEdgeDriver;
end;

procedure TestEdgeDriver.StartEdgeDriver;
var
  Edge:TEdgeDriver;
begin
  FWD :=TEdgeDriver.Create(nil);
  Edge :=FWD as TEdgeDriver;
  Edge.Port :=7777;
  Edge.Cmd :=FCMD;
  Edge.Timeout := 5000;
  Edge.DriverDir := ROOT_DIR + '\WebDriver\';
  Edge.StartDriver();

  Edge.NewSession();
  Edge.Implicitly_Wait(3000);
end;

procedure TestEdgeDriver.TearDown;
begin
  FreeAndNil(FWd);
  if Assigned(FCMD) then
    FreeAndNil(FCMD);
end;

function TestEdgeDriver.getValue(elem: TElement): string;
begin
   result := elem.PropertyHtml('value') ;
end;


{****************************************************************************}
initialization

// Register any test cases with the test runner
RegisterTest(TestFirefoxDriver.Suite);
RegisterTest(TestChromeDriver.Suite);
RegisterTest(TestEdgeDriver.Suite);

end.
